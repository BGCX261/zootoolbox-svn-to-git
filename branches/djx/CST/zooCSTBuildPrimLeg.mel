zooArrays_str;
zooAttrUtils;
zooBrandTools;
zooCSTUtils;
zooFlags;
zooObjMenuUtils;
zooCSTPrimitives;
zooSpaceSwitching;
zooRenamerUtils;
zooXferAnimUtils;


//script documentation is at the end of this file

// DJ MOD
//	Add "toggle knee control" rmb-menu
//	Use X_leg_ankle_loc instead of legControl for endObj in stretch to improve ball/toll roll behaviour
//		This requires a duplicate set of foot controls to be outside the FK chain so as to avoid a cycle
//	Add requirements for zooAlignIK()
//	Switch to use prim_qss
//	Turn off Animation Blending in maya prefs
// 	Add "reset to rig pose" obj menu
//  Force spline tangents in setDrivenKeyframe to avoid problems if user has step set in prefs
//	When switching ik/fk if the control handle is not the only thing selected there will be problems. Remove this limitation.
//	Improve the "switch to IK/FK for all keys" function - using the new zooSwitchForAllKeys.mel
//	Combine ball and toe roll into a single action, using utility nodes instead of SDKs
//	Additional option flags to support the heel-ball-toe roll system (see zooCSTBuildFootRoll.mel)
//	Add "switch roll to IK" obj menu
//	Add "switch IK to roll" obj menu
//	Add visible heel pivot with obj menu toggle to help foot planting

global proc string[] zooCSTBuildPrimLeg( string $prefix, string $parity, string $thigh, string $knee, string $ankle, string $optionStr ) {
	echoRigElementCreationCommand ( "zooCSTBuildPrimLeg \""+ $prefix +"\" "+ $parity +" "+ $thigh +" "+ $knee +" "+ $ankle +" \""+ $optionStr +"\";" );
	//------
	//parse optionStr to determine which flags are enabled for this proc
	//------
		string $optionTemp[] = {};
		string $parents[] = {};
		string $rootFlag = "";
		string $hipsFlag = "";
		string $ikType = "cube";
		string $colour = $parity=="r"? "red 0.65": "green 0.65";
		float $scale = 1;
		int $spaceswitching = 1;
		int $pickwalking = 1;
		int $stretch = 0;
		int $legacyRoll = `zooIsFlag $optionStr "legacyRoll"`;
		
		$optionTemp = `zooGetFlagArguments $optionStr root`; if( `size $optionTemp` ) $rootFlag = $optionTemp[0];
		$optionTemp = `zooGetFlagArguments $optionStr ikType`; if( `size $optionTemp` ) $ikType = $optionTemp[0];
		$optionTemp = `zooGetFlagArguments $optionStr hips`; if( `size $optionTemp` ) $hipsFlag = $optionTemp[0];
		$optionTemp = `zooGetFlagArguments $optionStr stretch`; if( `size $optionTemp` ) $stretch = $optionTemp[0];
		$optionTemp = `zooGetFlagArguments $optionStr spaceswitching`; if( `size $optionTemp` ) $spaceswitching = $optionTemp[0];
		$optionTemp = `zooGetFlagArguments $optionStr pickwalking`; if( `size $optionTemp` ) $pickwalking = $optionTemp[0];
		$optionTemp = `zooGetFlagArguments $optionStr colour`; if( `size $optionTemp` ) $colour = `zooArrayToStr_str $optionTemp " "`;
		$optionTemp = `zooGetFlagArguments $optionStr scale`; if( `size $optionTemp` ) $scale = $optionTemp[0];
		$optionTemp = `zooGetFlagArguments $optionStr parents`; if( `size $optionTemp` ) $parents = $optionTemp;
		
		// DJ MOD - additional options to support the heel-ball-toe roll system (see zooCSTBuildFootRoll.mel)
		float $ballBreak0 = 60.0;
		float $ballBreak1 = 90.0;
		float $heelMin = -90.0;
		float $toeMax = 90.0;
		int $sanityCheck = 1;

		$optionTemp = `zooGetFlagArguments $optionStr ballBreak0`; if( `size $optionTemp` ) $ballBreak0 = $optionTemp[0];
		$optionTemp = `zooGetFlagArguments $optionStr ballBreak1`; if( `size $optionTemp` ) $ballBreak1 = $optionTemp[0];
		$optionTemp = `zooGetFlagArguments $optionStr heelMin`; if( `size $optionTemp` ) $heelMin = $optionTemp[0];
		$optionTemp = `zooGetFlagArguments $optionStr toeMax`; if( `size $optionTemp` ) $toeMax = $optionTemp[0];
		$optionTemp = `zooGetFlagArguments $optionStr sanityCheck`; if( `size $optionTemp` ) $sanityCheck = $optionTemp[0];


		// DJ MOD
		// turn the blend pref off
		if(`optionVar -q animBlendingOpt`) {
			optionVar -iv animBlendingOpt 0;
			warning("zooCSTBuildPrimLeg: Animation Blending has been turned OFF in Maya prefs (zooCST requirement)");
		}


	//------
	//always build the world first - the world creation proc only builds the world once for any given prefix, so a world control is never created more than once
	//------
		string $suffix = ( $parity == "r" )?"_R":"_L";
		string $id = `zooCSTGetPrimId leg $suffix $prefix`;
		string $worldParts[] = `zooCSTBuildWorld $prefix $optionStr`;
		string $worldControl = $worldParts[0];
		string $partsControl = $worldParts[1];
		string $rootControl = zooGetElement_str( 0,`zooGetBrandedWith rootGimbal $prefix` );
		string $hipsControl = zooGetElement_str( 0,`zooGetBrandedWith hips $prefix` );
		string $masterqss = $worldParts[2];
		string $qss = $worldParts[3];

		if( `objExists $rootFlag` ) $rootControl = $rootFlag;
		if( `objExists $hipsFlag` ) $hipsControl = $hipsFlag;
		string $controls[] = `zooCSTBuildIKFK $prefix $parity $thigh $knee $ankle ( "-twistUpObj "+ $hipsControl +" -id "+ $id +" -alignEnd 0 "+ $optionStr )`;
		string $primitive = `zooCSTCreatePrimRecord leg $id $prefix`;
		cycleCheck -e 0;  //turn this off because when the script executes, it gives an annoying warning about a cycle that only exists for the duration of the script
		if( !`objExists $rootControl` ) warning "zooCSTBuildPrimLeg() no root control has been found, and no root control was specified on the command-line.  this may break this script - to manually specify a root control, please use the \"-root controlName\" flag when building this part of the rig";


	//------
	//rename the objects created by the ikfk proc
	//------
		for( $n=0; $n<`size $controls`; $n++ ) $controls[$n] = `zooRenamerFunctions replace limbUpper thigh $controls[$n]`;
		for( $n=0; $n<`size $controls`; $n++ ) $controls[$n] = `zooRenamerFunctions replace limbMiddle knee $controls[$n]`;
		for( $n=0; $n<`size $controls`; $n++ ) $controls[$n] = `zooRenamerFunctions replace limbEnd ankle $controls[$n]`;
		for( $n=0; $n<`size $controls`; $n++ ) $controls[$n] = `zooRenamerFunctions replace limb leg $controls[$n]`;
		for( $n=0; $n<`size $controls`; $n++ ) $controls[$n] = `zooRenamerFunctions replace pole knee $controls[$n]`;
		$controls[0] = `rename $controls[0] ( "leg_ik"+ $suffix )`;


	//------
	//create variables for each control used
	//------
		string $legIK = $controls[0];
		string $ikLegSpace = $controls[6];
		string $fkLegSpace = $controls[7];
		string $driverThigh = $controls[1];
		string $driverKnee = $controls[2];
		string $driverAnkle = $controls[3];
		string $ankleOrient = $controls[5];
		string $kneeControl = $controls[4];
		string $twistControl = $controls[14];
		string $legIKOrient = $controls[9];
		string $kneeControlSpace = $controls[11];//zooGetElement_str(0,`listRelatives -p $kneeControl`);
		string $toe = zooGetElement_str( 0,`listRelatives -c -pa -type joint $ankle` );
		string $toeTip = zooGetElement_str( 0,`listRelatives -c -pa -type joint $toe` );

	//------
	//build the objects to control the foot
	//------
		string $legControlSpace = `zooBuildControl ( "legSpace"+ $suffix ) ( "-type null -place "+ $ankle )`;
		string $legControl = `zooBuildControl ( "legControl"+ $suffix ) ( "-type "+ $ikType +" -place "+ $ankle +" -scale "+ $scale +" -colour "+ $colour )`;
		string $toeRoll;
		string $heelRoll = `zooBuildControl ( "heel_roll_piv"+ $suffix ) ( "-type null -place "+ $ankle +" -scale "+ $scale +" -offset -0.5fwd" )`;
		string $footControlSpace = `zooBuildControl ( "foot_controlSpace"+ $suffix ) ( "-type null -place "+ $ankle )`;
		string $footRollControl = `zooBuildControl ( "leg_roll_piv"+ $suffix ) ( "-type null -place "+ $toe )`;
		string $toeOrient = `zooBuildControl ( "leg_toe_orient_piv"+ $suffix ) ( "-type null -place "+ $toe )`;
		string $footBankL = `zooBuildControl ( "leg_bank_in_piv"+ $suffix ) ( "-type null -place "+ $toe +" -scale "+ $scale +" -offset "+($parity=="r"?1:-1)+"side" )`;
		string $footBankR = `zooBuildControl ( "leg_bank_out_piv"+ $suffix ) ( "-type null -place "+ $toe +" -scale "+ $scale +" -offset "+($parity=="r"?-1:1)+"side" )`;
		string $constraints[] = {};  //holds all constraints created by the script
		string $spaceNodes[] = {};  //holds all the space nodes created herein

		if( `objExists $toeTip` ) $toeRoll = `zooBuildControl ( "leg_toe_roll_piv"+ $suffix ) ( "-type null -place "+ $toeTip )`;
		else $toeRoll = `zooBuildControl ( "leg_toe_roll_piv"+ $suffix ) ( "-type null -place "+ $toe +" -scale "+ $scale +" -offset 1fwd" )`;
		select $heelRoll;  //stupid move command doesn't support object naming when specifying a single axis move, so we must selec the object first
		move -rpr -y 0;
		//move pivots to the ground
		eval( "move -a -y 0 0 0 "+ $footBankL +".rotatePivot "+ $footBankL +".scalePivot;" );
		eval( "move -a -y 0 0 0 "+ $footBankR +".rotatePivot "+ $footBankR +".scalePivot;" );
		
	// DJ MOD - make duplicate copies of everything below foot_control. Duplicate will be used for leg length calculation.
	//
	string $X_footControlSpace =  zooGetElement_str(0,`duplicate -rr -n ( "X_foot_controlSpace"+ $suffix ) $footControlSpace`);
	string $X_heelRoll =  zooGetElement_str(0,`duplicate -rr -n ( "X_heel_roll_piv"+ $suffix ) $heelRoll`);
	string $X_toeRoll =  zooGetElement_str(0,`duplicate -rr -n ( "X_leg_toe_roll_piv"+ $suffix ) $toeRoll`);
	string $X_footBankL =  zooGetElement_str(0,`duplicate -rr -n ( "X_leg_bank_in_piv"+ $suffix ) $footBankL`);
	string $X_footBankR =  zooGetElement_str(0,`duplicate -rr -n ( "X_leg_bank_out_piv"+ $suffix ) $footBankR`);
	string $X_footRollControl =  zooGetElement_str(0,`duplicate -rr -n ( "X_leg_roll_piv"+ $suffix ) $footRollControl`);
	string $X_toeOrient =  zooGetElement_str(0,`duplicate -rr -n ( "X_leg_toe_orient_piv"+ $suffix ) $toeOrient`);
	string $X_legAnkleLoc =  zooGetElement_str(0,`duplicate -rr -n ( "X_leg_ankle_loc"+ $suffix ) $X_footControlSpace`);

	// DJ MOD - need a way to track the toe pivot when legControl.toe rotates the toe.
	//
	string $toeTipPiv =  zooGetElement_str(0,`duplicate -rr -n ( "toe_tip_piv"+ $suffix ) $toeRoll`);
	parent $toeTipPiv $toeOrient;
	makeIdentity -a 1 $toeTipPiv;
	

	//------
	//parent the leg pivots together
	//------
		setAttr ( $legIK +".stickiness " ) 1;
		setAttr ( $legIK +".snapEnable " ) 0;
		if( `objExists $rootControl` ) parent $kneeControlSpace $rootControl;
		parent $heelRoll $footControlSpace;
		parent $toeRoll $heelRoll;
		parent $footBankL $toeRoll;
		parent $footBankR $footBankL;
		parent $footRollControl $footBankR;
		parent $toeOrient $footBankR;
		makeIdentity -apply 1 -t 1 -r 1 $heelRoll;
		
	// DJ MOD - parent the duplicate foot controls
	//
	parent $X_heelRoll $X_footControlSpace;
	parent $X_toeRoll $X_heelRoll;
	parent $X_footBankL $X_toeRoll;
	parent $X_footBankR $X_footBankL;
	parent $X_footRollControl $X_footBankR;
	parent $X_legAnkleLoc $X_footRollControl;
	parent $X_toeOrient $X_footBankR;
	makeIdentity -apply 1 -t 1 -r 1 $X_heelRoll;



	//------
	//re-parent the iklegSpace - so that its above the legControl, not the ikHandle
	//------
		parent $legIKOrient $footRollControl;
		parent $footControlSpace $legControl;
		// DJ MOD
		parent $X_footControlSpace $legControl;
		
		zooAddArray_str $constraints `orientConstraint -mo $toeOrient $toe`;

		delete `listConnections -d 0 ( $ankle +".rx" )`;
		parent $legControlSpace $worldControl;
		parent $legControl $legControlSpace;
		makeIdentity -apply 1 -t 1 -r 1 $legControl;
		orientConstraint -mo $legIK $ankle;



		// DJ MOD - Instead of using the _body_ctrls qss, create a qss for the controls in each primitive
		//
		string $controllers[] = {};
		$controllers = { $legControl, $kneeControl, $driverThigh, $driverKnee, $driverAnkle, $twistControl };
	
		string $prim_qss = zooCST_createQSS( $prefix +"_leg_ctrls"+ $suffix  );
		//string $prim_qss = zooCST_createQSS( $prefix +"_leg_ctrls");
		if(size($prim_qss)) sets -add $masterqss $prim_qss;
		else warning("zooCSTBuildPrimLeg: Failed to create qss called " + $prefix + "_leg_ctrls because the name clashed with an existing object.");

		// DJ MOD - Object Menu: add QSS selection and keying for each of the controllers
		//	Doing this early so that it comes at the top of the menu
		//
		zooAddSelectAndKeyObjectMenu($prefix, $prim_qss, $controllers, "-both");
		
	//------
	//build the knee setup
	//------
		string $kneePos = `group -em`;

		$kneePos = `rename $kneePos ( "knee_world_pos"+ $id )`;
		delete `parentConstraint $kneeControl $kneePos`;
		parent $kneePos $worldControl;
		pointConstraint -mo $rootControl $kneePos;
		if( $spaceswitching ) $spaceNodes = zooAddArray_str(`zooBuildSpaceSwitch $kneeControl $kneeControlSpace { $legControl, $rootControl, $kneePos } { "leg", "root", "root pos only" } "-mo "`,$spaceNodes);

		zooAttrState "-attrs t r s v -k 0 -l 1" $kneePos;
		makeIdentity -apply 1 -t 1 $kneeControl;


	//------
	//add attributes to the leg control, to control the pivots
	//------
		if($legacyRoll) {
			addAttr -k 1 -ln rollBall -at double -min 0 -max 10 $legControl;
			addAttr -k 1 -ln rollToe -at double -min -10 -max 10 $legControl;
		}
		else {
			// DJ MOD - Replace ballRoll and toeRoll with a single roll attribute and a ballBreaker.
			//			These could be added by zooCSTBuildFootRoll, but do it here so we get the attributes in a better order.
			//
			addAttr -k 1 -ln roll -at double -min -10 -max 10 $legControl;
			addAttr -k 1 -ln ballBreaker -at double -min -0 -max 1 -dv 1 $legControl;
		}
		
		addAttr -k 1 -ln twistFoot -at double -min -10 -max 10 $legControl;
		// DJ MOD - toe now in real units (degrees)
		addAttr -k 1 -ln toe -at double -min -90 -max 90 $legControl;
		addAttr -k 1 -ln bank -at double -min -10 -max 10 $legControl;

		if( `objExists ( $legIK +".switchUp" )`) addAttr -k 1 -ln switchUp -at bool $legControl;
		if( `objExists ( $legIK +".switchUp" )`) connectAttr -f ( $legControl +".switchUp" ) ( $legIK +".switchUp" );

		string $reverseNode = `shadingNode -asUtility reverse`;
		addAttr -k 1 -ln fkBlend -at double -min 0 -max 1 $legControl;
		connectAttr -f ( $legControl +".fkBlend" ) ( $reverseNode +".inputX" );
		connectAttr -f ( $reverseNode +".outputX" ) ( $legIK +".ikBlend" );


	//------
	//set up the parent constraint for the reverse foot setup - so that it blends to the fk control when its enabled
	//------
		string $footControlConstraint = zooGetElement_str(0,`parentConstraint -st x -st y -st z -mo $legControl $driverAnkle $footControlSpace`);
		string $constraintAttrs[] = `listAttr -ud $footControlConstraint`;
		string $weightReverse = `shadingNode -asUtility reverse`;
		connectAttr -f ( $legControl +".fkBlend" ) ( $footControlConstraint +"."+ $constraintAttrs[1] );
		connectAttr -f ( $legControl +".fkBlend" ) ( $weightReverse +".inputX" );
		connectAttr -f ( $weightReverse +".outputX" ) ( $footControlConstraint +"."+ $constraintAttrs[0] );
		setAttr ( $footControlConstraint +".interpType" ) 2;


	//------
	//build the SDK's to control the pivots
	//because these objects are built by this script, we know which axes to drive, without using zooCSTUpVector
	//------
		// DJ MOD - force spline tangents to avoid problems if user has step set in prefs
		//			By default, combine ball and toe roll into a single action, using utility nodes instead of SDKs
		//
		string $rollBallAxis = `zooCSTUpVectorAsName $footRollControl {1.,0.,0.}`;
		string $toeRollAxis = `zooCSTUpVectorAsName $toeRoll {1.,0.,0.}`;
		
		if($legacyRoll) {
			// this is the old SDK foot roll method
			setDrivenKeyframe -itt "spline" -ott "spline" -cd ( $legControl +".rollBall" ) -dv 0 -v 0 ( $footRollControl +".r"+ $rollBallAxis);
			setDrivenKeyframe -itt "spline" -ott "spline" -cd ( $legControl +".rollBall" ) -dv 10 -v 90 ( $footRollControl +".r"+ $rollBallAxis );
			setDrivenKeyframe -itt "spline" -ott "spline" -cd ( $legControl +".rollBall" ) -dv -10 -v -90 ( $footRollControl +".r"+ $rollBallAxis );

			setDrivenKeyframe -itt "spline" -ott "spline" -cd ( $legControl +".rollToe" ) -dv 0 -v 0 ( $toeRoll +".r"+ $toeRollAxis );
			setDrivenKeyframe -itt "spline" -ott "spline" -cd ( $legControl +".rollToe" ) -dv 10 -v 90 ( $toeRoll +".r"+ $toeRollAxis );
			setDrivenKeyframe -itt "spline" -ott "spline" -cd ( $legControl +".rollToe" ) -dv 0 -v 0 ( $heelRoll +".r"+ $toeRollAxis );
			setDrivenKeyframe -itt "spline" -ott "spline" -cd ( $legControl +".rollToe" ) -dv -10 -v -90 ( $heelRoll +".r"+ $toeRollAxis );
		}
		else {
			// DJ MOD - set up the new ballBreaker foot roll system
			zooCSTBuildFootRoll $legControl $heelRoll $footRollControl $toeRoll ("-ballBreak0 "+ $ballBreak0 +" -ballBreak1 "+ $ballBreak1 +" -heelMin "+ $heelMin +" -toeMax "+ $toeMax +" -heelRollAxis "+ $toeRollAxis +" -ballRollAxis "+ $rollBallAxis +" -toeRollAxis "+ $toeRollAxis +" -sanityCheck "+ $sanityCheck);
		}
		
		// DJ MOD - toggleable visible heel pivot to help IK foot plant (this is not really a control, just a visual aid)
		string $heelRollControl = `zooBuildControl ( "heelRollControl"+ $id ) ( "-type sphere -place "+ $heelRoll +" -colour "+ $colour +" -scale "+ ($scale*0.1))`;
		parent $heelRollControl $heelRoll;
		makeIdentity -apply 1 -t 1 -r 1 $heelRollControl;
		zooAttrState "-attrs t r -k 0 -l 1" $heelRollControl;
		
		string $toeTwistAxis = `zooCSTUpVectorAsName $toeRoll (zooCSTGetDir("up"))`;
		setDrivenKeyframe -itt "spline" -ott "spline" -cd ( $legControl +".twistFoot" ) -dv -10 -v -90 ( $toeRoll +".r"+ $toeTwistAxis );
		setDrivenKeyframe -itt "spline" -ott "spline" -cd ( $legControl +".twistFoot" ) -dv 10 -v 90 ( $toeRoll +".r"+ $toeTwistAxis );

		// DJ MOD - toe now in real units (degrees)
		string $toeAxis = `zooCSTUpVectorAsName $toeOrient {1.,0.,0.}`;
		setDrivenKeyframe -itt "spline" -ott "spline" -cd ( $legControl +".toe" ) -dv 90 -v 90 ( $toeOrient +".r"+ $toeAxis );
		setDrivenKeyframe -itt "spline" -ott "spline" -cd ( $legControl +".toe" ) -dv -90 -v -90 ( $toeOrient +".r"+ $toeAxis );

		float $min = ($parity=="r")? 90: -90;
		float $max = ($parity=="r")? -90: 90;
		string $bankAxisL = `zooCSTUpVectorAsName $footBankL (zooCSTGetDir("fwd"))`;
		string $bankAxisR = `zooCSTUpVectorAsName $footBankR (zooCSTGetDir("fwd"))`;
		setDrivenKeyframe -itt "spline" -ott "spline" -cd ( $legControl +".bank" ) -dv 0 -v 0 ( $footBankL +".r"+ $bankAxisL );
		setDrivenKeyframe -itt "spline" -ott "spline" -cd ( $legControl +".bank" ) -dv 10 -v $max ( $footBankL +".r"+ $bankAxisL );
		setDrivenKeyframe -itt "spline" -ott "spline" -cd ( $legControl +".bank" ) -dv 0 -v 0 ( $footBankR +".r"+ $bankAxisR );
		setDrivenKeyframe -itt "spline" -ott "spline" -cd ( $legControl +".bank" ) -dv -10 -v $min ( $footBankR +".r"+ $bankAxisR );

	// DJ MOD - connect the setDrivenAnim nodes to the duplicate foot controls so they will follow along
	//
	connectAttr -f (zooGetElement_str(0,`listConnections -d 0 ($footRollControl +".r"+ $rollBallAxis)`)+".output")  ($X_footRollControl +".r"+ $rollBallAxis);
	connectAttr -f (zooGetElement_str(0,`listConnections -d 0 ($toeRoll +".r"+ $toeRollAxis)`)+".output")  ($X_toeRoll +".r"+ $toeRollAxis);
	connectAttr -f (zooGetElement_str(0,`listConnections -d 0 ($heelRoll +".r"+ $toeRollAxis)`)+".output")  ($X_heelRoll +".r"+ $toeRollAxis);
	connectAttr -f (zooGetElement_str(0,`listConnections -d 0 ($toeRoll +".r"+ $toeTwistAxis)`)+".output")  ($X_toeRoll +".r"+ $toeTwistAxis);
	connectAttr -f (zooGetElement_str(0,`listConnections -d 0 ($toeOrient +".r"+ $toeAxis)`)+".output")  ($X_toeOrient +".r"+ $toeAxis);
	connectAttr -f (zooGetElement_str(0,`listConnections -d 0 ($footBankL +".r"+ $bankAxisL)`)+".output")  ($X_footBankL +".r"+ $bankAxisL);
	connectAttr -f (zooGetElement_str(0,`listConnections -d 0 ($footBankR +".r"+ $bankAxisR)`)+".output")  ($X_footBankR +".r"+ $bankAxisR);
	
	//------
	//build space switching expressions
	//------
		string $legControlParents[] = { $worldControl, $rootControl, $hipsControl };
		if( `objExists $hipsControl` ) parent $fkLegSpace $hipsControl;
		else parent $fkLegSpace $rootControl;
		if( `size $parents` ) zooAddArray_str $legControlParents $parents;
		if( $spaceswitching ) zooAddArray_str $spaceNodes `zooBuildSpaceSwitch $legControl $legControlSpace $legControlParents { "world", "root", "hips" } "-mo"`;
		if( $spaceswitching ) zooAddArray_str $spaceNodes `zooBuildSpaceSwitch $driverThigh $fkLegSpace { $hipsControl, $rootControl, $worldControl } { "hips", "root", "world" } "-mo"`;

		// DJ MOD - dont know what these do. Comment out to avoid warning message
		//delete -icn ( $fkLegSpace +".tx" );
		//delete -icn ( $fkLegSpace +".ty" );
		//delete -icn ( $fkLegSpace +".tz" );


	//------
	//build pickwalking if required
	//------
		if( $pickwalking ) {
			zooPickwalk "-load";
			string $parityName = "left";
			if( $parity == "r" ) $parityName = "right";
			zooSetPickwalkObj $driverThigh $rootControl "-dir up -reciprocal 0";
			zooSetPickwalkObj $driverThigh $driverKnee "-dir down -reciprocal 1";
			zooSetPickwalkObj $driverKnee $driverAnkle "-dir down -reciprocal 1";
			zooSetPickwalkObj $driverAnkle $legControl "-dir left -reciprocal 1";
			zooSetPickwalkObj $driverAnkle $legControl "-dir right -reciprocal 1";
			zooSetPickwalkObj $legControl $kneeControl "-dir up -reciprocal 1";
			zooSetPickwalkObj $legControl $kneeControl "-dir down -reciprocal 1";
			zooSetPickwalkObj $rootControl $legControl ( "-dir "+ $parityName+ " -reciprocal 0" );
			}


	//------
	//add the zooObjMenu commands to the object for easy switching to FK mode
	//------
		int $idx_eff = `zooAddConnect $legControl $legIK`;
		int $idx_kne = `zooAddConnect $legControl $kneeControl`;

		// DJ MOD - some extra connects for the new "switch to IK for all keys" method
		int $idx_dan = `zooAddConnect $legControl $driverAnkle`;
		int $idx_dkn = `zooAddConnect $legControl $driverKnee`;
		int $idx_dth = `zooAddConnect $legControl $driverThigh`;
		
		zooObjMenuAddCmdAll $legControl "switch to FK" ( "string $sel[] = `ls -sl`;\nselect #;\nzooFlags;\nzooAlignFK \"-control # -ikHandle %"+ $idx_eff +" -onCmd setAttr #.fkBlend 0 -offCmd setAttr #.fkBlend 1\";\nselect $sel;" );

		// DJ MOD - new "switch for all keys" method
		//zooObjMenuAddCmdAll $legControl "switch to FK for all keys" ( "string $sel[] = `ls -sl`;\nselect %0 %"+ $idx_kne +";\nsource zooKeyCommandsWin;\ncheckBox -e -v 1 zooKeyCommandsCheckCombine;\ncheckBox -e -v 1 zooKeyCommandsCheckSolo;\nzooSetKeyCommandsWindowCmd \"if(`getAttr %0.fkBlend` != 1) {\\n\\tsetKeyframe -at fkBlend %0;\\n}\\n&&&\\nif(`getAttr %0.fkBlend` != 1) {\\n\\tselect %0;\\n\\tzooFlags;\\n\\tzooAlign \\\"\\\";\\n\\tzooAlignFK \\\"-control %0 -ikHandle %"+ $idx_eff +" -onCmd setAttr %0.fkBlend 0 -offCmd setAttr %0.fkBlend 1\\\";\\n}\";" );
		//zooObjMenuAddCmdAll $legControl "switch to IK for all keys" ( "string $sel[] = `ls -sl`;\nselect %"+ $idx_dan +" %"+ $idx_dkn +" %"+ $idx_dth +";\nsource zooKeyCommandsWin;\ncheckBox -e -v 1 zooKeyCommandsCheckCombine;\ncheckBox -e -v 1 zooKeyCommandsCheckSolo;\nzooSetKeyCommandsWindowCmd \"if(`getAttr %0.fkBlend` != 0) {\\n\\tsetKeyframe -at fkBlend %0;\\n}\\n&&&\\nif(`getAttr %0.fkBlend` != 0) {\\n\\tselect %"+ $idx_dan +";\\n\\tzooFlags;\\n\\tzooAlign \\\"\\\";\\n\\tzooAlignIK \\\"-control %0 -ikHandle %"+ $idx_eff +" -pole %"+ $idx_kne +" -onCmd setAttr %0.fkBlend 0 -offCmd setAttr %0.fkBlend 1\\\";\\n}\";" );
		zooObjMenuAddCmdAll $legControl "switch to FK for all keys" ("source zooKeyCommandsWin;\nzooSwitchForAllKeys \"-mode fk -blendAttr fkBlend -ik_ctl # -ik_eff %"+ $idx_eff +" -selObjs %"+ $idx_kne +"\";");
		zooObjMenuAddCmdAll $legControl "switch to IK for all keys" ("source zooKeyCommandsWin;\nzooSwitchForAllKeys \"-mode ik -blendAttr fkBlend -ik_ctl # -ik_eff %"+ $idx_eff +" -ik_mid %"+ $idx_kne +" -fk_end %"+ $idx_dan +" -selObjs %"+ $idx_dkn +" %"+ $idx_dth +"\";");


		// DJ MOD - we dont need line of action
		//zooObjMenuAddCmdAll $legControl "draw line of action" "zooBrandTools;\nzooCSTPrimitives;\nzooLineOfAction;\nstring $prim = `zooGetPrimFromRegistered #`;\nstring $inputs[] = `zooCSTListRegistered input $prim`;\nzooLineOfAction_multi $inputs \"\";";

		int $idx_ctl = `zooAddConnect $driverAnkle $legControl`;
		int $idx_eff = `zooAddConnect $driverAnkle $legIK`;
		int $idx_kne = `zooAddConnect $driverAnkle $kneeControl`;
		zooObjMenuAddCmdAll $driverAnkle "switch to IK" ( "string $sel[] = `ls -sl`;\nselect #;\nzooFlags;\nzooAlign \"\";\nzooAlignIK \"-control %"+ $idx_ctl +" -ikHandle %"+ $idx_eff +" -pole %"+ $idx_kne +" -offCmd setAttr %"+ $idx_ctl +".fkBlend 0\";\nselect $sel;" );

		// DJ MOD add "switch for all keys" menu
		//zooObjMenuAddCmdAll $driverAnkle "switch to IK for all keys" ( "string $sel[] = `ls -sl`;\nselect %0 %1 %2;\nsource zooKeyCommandsWin;\ncheckBox -e -v 1 zooKeyCommandsCheckCombine;\ncheckBox -e -v 1 zooKeyCommandsCheckSolo;\nzooSetKeyCommandsWindowCmd \"if(`getAttr %"+ $idx_ctl +".fkBlend` != 0) {\\n\\tsetKeyframe -at fkBlend %"+ $idx_ctl +";\\n}\\n&&&\\nif(`getAttr %"+ $idx_ctl +".fkBlend` != 0) {\\n\\tselect %0;\\n\\tzooFlags;\\n\\tzooAlign \\\"\\\";\\n\\tzooAlignIK \\\"-control %"+ $idx_ctl +" -ikHandle %"+ $idx_eff +" -pole %"+ $idx_kne +" -offCmd setAttr %"+ $idx_ctl +".fkBlend 0\\\";\\n}\";" );
		//zooObjMenuAddCmdAll $driverAnkle "switch to FK for all keys" ( "string $sel[] = `ls -sl`;\nselect %"+ $idx_ctl +" %"+ $idx_kne +";\nsource zooKeyCommandsWin;\ncheckBox -e -v 1 zooKeyCommandsCheckCombine;\ncheckBox -e -v 1 zooKeyCommandsCheckSolo;\nzooSetKeyCommandsWindowCmd \"if(`getAttr %"+ $idx_ctl+ ".fkBlend` != 1) {\\n\\tsetKeyframe -at fkBlend %"+ $idx_ctl +";\\n}\\n&&&\\nif(`getAttr %"+ $idx_ctl +".fkBlend` != 1) {\\n\\tselect %"+ $idx_ctl +";\\n\\tzooFlags;\\n\\tzooAlign \\\"\\\";\\n\\tzooAlignFK \\\"-control %"+ $idx_ctl +" -ikHandle %"+ $idx_eff +" -offCmd setAttr %"+ $idx_ctl +".fkBlend 0\\\";\\n}\";" );
		zooObjMenuAddCmdAll $driverAnkle "switch to IK for all keys" ("source zooKeyCommandsWin;\nzooSwitchForAllKeys \"-mode ik -blendAttr fkBlend -ik_ctl %"+ $idx_ctl +" -ik_eff %"+ $idx_eff +" -ik_mid %"+ $idx_kne +" -fk_end %0 -selObjs %1 %2\";");
		zooObjMenuAddCmdAll $driverAnkle "switch to FK for all keys" ("source zooKeyCommandsWin;\nzooSwitchForAllKeys \"-mode fk -blendAttr fkBlend -ik_ctl %"+ $idx_ctl +" -ik_eff %"+ $idx_eff +" -selObjs %"+ $idx_kne +"\";");

		for( $obj in {$legControl,$kneeControl,$driverThigh,$driverKnee,$driverAnkle} ) setObjMenuKillState $obj 1;
		
		// DJ MOD - "switch roll to ik" & "switch ik to roll" obj menus
		//			Makes these assumptions about the way the foot is rigged:
		//				- .toe is specified in degrees, not the usual -10 to 10 range
		//				- .roll range [-10,10]
		//				- legControl.roll equivalent as IK uses only the x-axis
		//			
		if(!$legacyRoll) {
			int $idx_frc = `zooAddConnect $legControl $footRollControl`;
			int $idx_hrc = `zooAddConnect $legControl $heelRoll`;
			int $idx_trc = `zooAddConnect $legControl $toeTipPiv`;
			
			int $idx_eff = zooGetElement_int (0, `zooGetConnectSlots $legControl $legIK`);
			
			zooObjMenuAddCmdAll $legControl "switch roll to IK" ( "string $sel[] = `ls -sl`;\nzooAlign \"-load 1\";\nzooAlignLegRollToIK # %"+ $idx_eff +" %"+ $idx_frc +" \"-rollBallAxis "+ $rollBallAxis +" -key\";\nselect $sel;" );
			zooObjMenuAddCmdAll $legControl "switch IK to roll" ( "string $sel[] = `ls -sl`;\nzooAlign \"-load 1\";\nzooAlignLegIKToRoll # %"+ $idx_hrc +" %"+ $idx_frc +" %"+ $idx_trc +" \"-key\";\nselect $sel;" );
		}


	//------
	//add known rig-skeleton offsets to feet controls (feet are the only rig controls that are oriented differently to the
	//underlying skeletal components, so we store this offset data on the controls so tracing works properly
	//------
		float $offset[] = {0,0,0};
		zooAlignFast $ankle $legControl 0 0;
		$offset = `getAttr ( $legControl +".r" )`;
		for( $i=0; $i<3; $i++ ) $offset[$i] *= -1;  //negate the rotational offset
		setAttr ( $legControl +".t" ) 0 0 0;
		setAttr ( $legControl +".r" ) 0 0 0;
		zooSetPostTraceCmd $legControl ( "rotate -r -os "+ $offset[0] +" "+ $offset[1] +" "+ $offset[2] +" #; setKeyframe -at r #;" );
		zooSetPostTraceCmd $legControl ( "rotate -r -os "+ $offset[0] +" "+ $offset[1] +" "+ $offset[2] +" #; setKeyframe -at r #;" );


	//------
	//make the limb stretchy?
	//------

		// DJ MOD
		//	If endObj = legControl then leg fails to stretch correctly during ball/toe roll
		//	Use X_leg_ankle_loc on a duplicate foot instead. (Duplicate outside of FK chain required to avoid cycle)
		//
		string $legIK_alignSpace[] = `listRelatives -p $legIK`;
		
		string $stretchNodes[] = {};
		if( $stretch ) {
			$stretchNodes = `zooCSTMakeStretchy $legControl $legIK ( $optionStr +" -startObj "+ $thigh +" -endObj "+ $X_legAnkleLoc +" -register 1 -primitive "+ $primitive +" -axis "+ zooCSTJointDirection($ankle) +" -prefix "+ $prefix +" -parts "+ $partsControl )`;
			renameAttr ( $legControl +".elbowPos" ) kneePos;
			}
			
		// DJ MOD add fix for zooAlignIK
		//
		string $loc = `group -em -p $legControlSpace -n ($legControl+"_loc")`;
		delete `parentConstraint $legControl $loc`;
		makeIdentity -a true $loc;
		addAttr -ln "zooLoc" -at message $legControl;
		connectAttr -f ($loc+".message") ($legControl+".zooLoc");


	//------
	//hide attribs, objects and cleanup
	//------
		delete `listRelatives -s -pa $legIK`;  //delete legIK shape nodes
		zooAttrState "-attrs s kneePos -k 0 -l 1" $legControl;
		zooAttrState "-attrs v -k 0" $legControl;
		//get the condition node that drives ik control visibility, and connect it to the actual leg control's vis
		string $cond = zooGetElement_str(0,`listConnections -d 0 ( $legIK +".v" )`);
		connectAttr -f ( $cond +".outColorG" ) ( $legControlSpace +".v" );
		string $toLock[] = {$legIK,$legControlSpace,$toeRoll,$heelRoll,$footControlSpace,$footRollControl,$toeOrient,$footBankL,$footBankR};
		for( $a in $toLock ) zooAttrState "-attrs t r s -l 1" $a;

		string $toLock[] = {$X_footControlSpace,$X_heelRoll,$X_toeRoll,$X_footBankL,$X_footBankR,$X_footRollControl,$X_toeOrient,$X_legAnkleLoc};
		for( $a in $toLock ) zooAttrState "-attrs t r s -l 1" $a;

		string $toLock[] = {$driverKnee,$driverAnkle};
		for( $a in $toLock ) zooAttrState "-attrs t s -l 1" $a;

	//------
	//now brand all objects
	//------
		string $allSetupObjects[] = { $legControl, $kneeControl, $driverThigh, $driverKnee, $driverAnkle };
		for( $obj in $allSetupObjects ) zooBrandObject name $prefix $obj;
		zooBrandObject leg $suffix $legControl;
		zooBrandObject knee $suffix $kneeControl;
		zooBrandObject fk_thigh $suffix $driverThigh;
		zooBrandObject fk_knee $suffix $driverKnee;
		zooBrandObject fk_ankle $suffix $driverAnkle;


	//------
	//create a list of controls
	//------
		// DJ MOD - we did this already, but do it again in case the names changed
		string $controllers[];
		$controllers = { $legControl, $kneeControl, $driverThigh, $driverKnee, $driverAnkle, $twistControl };
		
		for( $obj in $controllers ) {
			if( `objExists $obj` ) {
				// Add controls to prim qss
				sets -add $prim_qss $obj;
				
				// DJ MOD - reset pose to default
				int $qssIdx = `zooAddConnect $obj $prim_qss`;
				zooObjMenuAddCmdAll $obj "reset to rigpose" ( "zooUtils;\nstring $all[] = `zooQuerySet %"+ $qssIdx +" 0 1`;\nfor( $a in $all ) zooResetAttrs $a;" );
			}
		}
		
	// DJ MOD RMB menu toggles
		zooObjMenuAddCmdAll $legControl "toggle knee control" "{\nstring $kneeCtrl[] = `listConnections #.zooTrig2`;\nsetAttr ($kneeCtrl[0] +\".v\") (!`getAttr ($kneeCtrl[0] +\".v\")`);\n}";
		int $idx_ctl = `zooAddConnect $legControl $heelRollControl`;
		zooObjMenuAddCmdAll $legControl "toggle heel pivot" ("setAttr %"+ $idx_ctl +".v (!`getAttr %"+ $idx_ctl +".v`);\n");
		
	//------
	//build the primitive record
	//------
		setAttr ( $kneeControl +".v" ) 0;
		zooCSTRegisterInput $thigh $primitive;
		zooCSTRegisterInput $knee $primitive;
		zooCSTRegisterInput $ankle $primitive;
		zooCSTPrimOutputs $controls $primitive;
		zooCSTPrimOutputs { $legControl, $legControlSpace, $footControlSpace, $footRollControl, $toeOrient } $primitive;
		zooCSTPrimOutputs { $footBankL, $footBankR, $toeRoll } $primitive;
		zooCSTPrimOutputs { $reverseNode, $weightReverse, $footControlConstraint } $primitive;
		zooCSTPrimOutputs $constraints $primitive;
		zooCSTPrimOutputs $stretchNodes $primitive;
		zooCSTPrimOutputs $spaceNodes $primitive;
		zooBrandObject options ( $optionStr +" -parity "+ $parity ) $primitive;
		zooBrandObject version "6" $primitive;
		for( $obj in $controllers ) zooCSTRegisterControl $obj $primitive;
		// DJ MOD
		zooCSTPrimOutputs {$loc} $primitive;

		delete $ankleOrient;  //this isn't used in the leg
		cycleCheck -e 1;

		zooCSTUtilsRigVis -h;
		for( $n=0; $n<`size $controllers`; $n++ ) $controllers[$n] = `zooRenamerFunctions replace "[0-9]+$" "" $controllers[$n]`;
		
		// DJ MOD - don't select anything
		//select $prim_qss;

	return $controllers;
	}


//------
//this proc deals with rebuilding the leg primitive
//------
global proc string[] zooCSTRebuildPrimLeg( string $primitive ) {
	string $prefix = `zooQueryBrand $primitive char`;
	string $parity = "l";
	string $version = `zooQueryBrand version $primitive`;
	string $optionStr = `zooQueryBrand $primitive options`;
	string $inputs[] = `zooCSTListRegistered input $primitive`;
	string $controls[] = `zooCSTListRegistered control $primitive`;
	string $temp[] = {};

	string $dupes[] = {};
	if( `size $controls` ) {
		$dupes = `duplicate -rc $controls`;  //duplicate the control objects so we can grab the original control shape nodes, to parent under the new controls created
		parent -w $dupes;
		}

	$temp = `zooGetFlagArguments $optionStr parity`; if( `size $temp` ) $parity = $temp[0];

	zooCSTDeletePrimitive $primitive;
	string $newControls[] = `zooCSTBuildPrimLeg $prefix $parity $inputs[0] $inputs[1] $inputs[2] $optionStr`;

	//parent the shape nodes of the original controls to the transforms of the new controls
	for( $n=0; $n<`size $newControls`; $n++ ) zooReplaceShape $dupes[$n] $newControls[$n] 0;

	delete $dupes;
	return $newControls;
	}


/*
builds a generic IK/FK leg rig
-------

$prefix - the character prefix used to identify the character
$parity - which side is the arm on?  l (left) or r (right)
$thigh - the thigh, or femur
$knee - the knee, or tibia/fibula.  ie where the leg bends
$ankle - the ankle/foot joint
$optionStr - standard option string - see technical docs for info on option strings

option flags
-------
-root [string]			use this to override which object is used as the root control
-hips [string]			use this to override which object is used as the hips control
-stretch [int]			turn this flag on if you want a stretchy leg
-parents [string]		use this to specify an additional list of parents for the dynamic parenting
-scale [float]			scale factor to apply to the control (purely a visual thing - scale is frozen).  default: 1
*/

//zoo.end
//macaroniKazoo ©2007